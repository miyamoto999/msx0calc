10 ' 計算機
20 ' 初期化
30 SCREEN 5,,0:COLOR 15,4,7:DEFINT A-Z:SET PAGE 0,1
40 DIM K(3,4)
50 X0=-1:Y0=-1:AN$="0":PF=0:VC=1:AF=0:CA=-1:MO=0:EQ=0:A=0
60 ' フォントデータの読み込み
70 COPY "font12.dat" TO (0,0):' 12x16フォント(0123456789+-×÷=±ACER. )
80 COPY "font16.dat" TO (0,32):' 16x16フォント(0123456789+-×÷=±ACER. )
90 COPY "font8.dat" TO (0,64):' 8x8フォント(+-×÷ )
100 SET PAGE 0,0
110 ' 画面上のテンキーに対応するキー情報を初期化
120 _TURBO ON(K())
130 RESTORE 1510
140 FOR Y=0 TO 4
150     FOR X=0 TO 3
160         READ A$
170         IF A$<>"" THEN K(X,Y)=ASC(A$) ELSE K(X,Y)=0
180     NEXT X
190 NEXT Y
200 ' 画面表示
210 FOR Y=27 TO 211 STEP 37
220     FOR X = 0 TO 255 STEP 64
230         LINE (0,Y)-(255,Y)
240         LINE (X,27)-(X, 211)
250         READ A$
260         IF A$="" THEN GOTO 290
270         IF A$="AC" THEN GOSUB 340:GOTO 290
280         GOSUB 390
290     NEXT X
300 NEXT Y
310 GOTO 410
320 ' "AC"を描画
330 ' x,y:テンキーの座標(4x5)
340 COPY(0,48)-STEP(15,15),1 TO (X+16,Y+10):COPY(16,48)-STEP(15,15),1 TO (X+32,Y+10)
350 RETURN
360 ' テンキーの文字を描画
370 ' x,y:テンキーの座標(4x5)
380 ' a:描画する文字のJIS漢字コード
390 A=VAL(A$):XX=A*16 MOD 256:Y1=A*16:Y2=Y1/256:YY=Y2*16+32:COPY(XX,YY)-STEP(15,15),1 TO (X+24,Y+10)
400 RETURN
410 _TURBO OFF
420 MS$=AN$:GOSUB 850
430 ' キー入力&パッド入力
440 _TURBO ON(A,K(),X0,Y0)
450 MO=0
460 P=PAD(4)
470 A$=INKEY$
480 IF X0<>-1 THEN GOSUB 620:X0=-1
490 IF P<>0 THEN MO=1
500 IF A$<>"" THEN MO=2
510 IF MO=0 THEN 450
520 IF MO=1 THEN X0=PAD(5)/64:Y0=(PAD(6)-27)/37:A$=CHR$(K(X0,Y0)):GOSUB 620
530 IF A$=CHR$(&H1B) THEN A$="c"
540 IF A$=CHR$(13) THEN A$="="
550 IF MO=2 THEN GOSUB 660:GOSUB 620
560 IF A$="" OR A$=CHR$(0) THEN 450
570 A=ASC(A$)
580 GOTO 730
590 ' ボタンの反転描画
600 ' x0,y0:テンキーの座標(4x5)
610 ' x0==-1の時はなにもしない。
620 IF X0=-1 THEN RETURN
630 LINE(X0*64+1,Y0*37+27)-STEP(62,35),15,BF,XOR
640 RETURN
650 ' キーチェック
660 KK=ASC(A$)
670 FOR Y=0 TO 4
680     FOR X=0 TO 3
690         IF K(X,Y)=KK THEN X0=X:Y0=Y
700     NEXT X
710 NEXT Y
720 RETURN
730 _TURBO OFF
740 A$=CHR$(A)
750 IF A$>="0" AND A$<="9" THEN GOSUB 1060:P$=AN$:GOSUB 1160:MS$=P$:GOSUB 850:GOTO 440
760 IF A$="." AND PF=0 THEN PF=1:IF VC=1 THEN AN$="0.":VC=0:MS$=AN$:GOSUB 850 ELSE AN$=AN$+".":P$=AN$:GOSUB 1160:MS$=P$:GOSUB 850
770 IF A$="c" THEN IF VC=0 THEN AN$="0":PF=0:VC=1:AF=0:MS$=AN$:GOSUB 850 ELSE AN$="0":PF=0:VC=1:CA=-1:V0#=0:AF=0:MS$=AN$:GOSUB 850
780 IF A$="*" OR A$="/" OR A$="+" OR A$="-" THEN EQ=0:IF VC=0 THEN VC=1:GOSUB 1210 ELSE GOSUB 1440:GOSUB 850
790 IF A$="=" THEN EQ=1:VC=1:GOSUB 1210
800 IF A$="!" THEN GOSUB 1380:P$=AN$:GOSUB 1160:MS$=P$:GOSUB 850
810 IF A$="q" THEN END
820 GOTO 440
830 ' 値を表示するエリアを再描画
840 ' ms$:描画する文字列
850 P0=VARPTR(MS$):L=PEEK(P0):P=0:P2=VARPTR(P):POKE P2,PEEK(P0+1):POKE P2+1,PEEK(P0+2)
860 _TURBO ON(P,L,CA)
870 SX=240-L*12:LINE(0,0)-(SX-1,26),4,BF
880 FOR I=1 TO L
890     C=PEEK(P+I-1)
900     IF C=&H45 THEN X=216:Y=0:GOTO 970
910     IF C=&H52 THEN X=228:Y=0:GOTO 970                   
920     IF C>=&H30 THEN X=(C-&H30)*12:Y=0:GOTO 970
930     IF C=&H2B THEN X=120:Y=0:GOTO 970
940     IF C=&H2D THEN X=132:Y=0:GOTO 970
950     IF C=&H2E THEN X=240:Y=0:GOTO 970
960     IF C=&H20 THEN X=0:Y=16:GOTO 970
970     COPY(X,Y)-STEP(11,15),1 TO (SX, 4)
980     SX=SX+12
990 NEXT I
1000 IF CA=-1 THEN A=32 ELSE A=CA*8
1010 COPY(A,64)-STEP(7,7),1 TO (244,12)
1020 _TURBO OFF
1030 RETURN
1040 ' an$に入力した数値を追加する
1050 ' a$:追加する数値or少数点
1060 IF EQ=1 THEN CA=-1:EQ=0
1070 IF VC=1 THEN AN$=A$:AF=1:IF A$="0" THEN VC=1:RETURN ELSE VC=0:RETURN
1080 LL=LEN(AN$):MM$=MID$(AN$,1,1)
1090 IF LL=14 AND MM$<>"-" THEN RETURN
1100 IF LL=15 AND MM$="-" THEN RETURN
1110 AN$=AN$+A$
1120 AF=1
1130 RETURN
1140 ' 少数点で始まる場合、前に0を追加する
1150 ' P$:数値の文字列
1160 IF MID$(P$,1,1)=" " THEN P$=MID$(P$,2)
1170 IF MID$(P$,1,1)="-" THEN SP$="-":P$=MID$(P$,2) ELSE SP$=""
1180 IF MID$(P$,1,1)="." THEN P$="0"+P$
1190 P$=SP$+P$:RETURN
1200 ' 演算処理
1210 IF AF=1 THEN AN#=VAL(AN$)
1220 EF=0:' エラーフラグ
1230 IF CA=-1 THEN V0#=AN# ELSE IF A$<>"=" AND AF=0 THEN GOSUB 1450 ELSE GOSUB 1290
1240 VC=1:PF=0:AF=0
1250 IF EF=1 THEN MS$="ERR":GOSUB 850:RETURN
1260 IF A$<>"=" THEN GOSUB 1440
1270 P$=STR$(V0#):GOSUB 1160:AN$=P$:MS$=P$:GOSUB 850
1280 RETURN
1290 ON ERROR GOTO 1360
1300 IF CA=0 THEN V0#=V0#+AN#:GOTO 1340
1310 IF CA=1 THEN V0#=V0#-AN#:GOTO 1340
1320 IF CA=2 THEN V0#=V0#*AN#:GOTO 1340
1330 IF CA=3 THEN V0#=V0#/AN#:GOTO 1340
1340 ON ERROR GOTO 0
1350 RETURN
1360 EF=1:RESUME NEXT
1370 ' 符号反転の処理("!"を押した時)
1380 IF AF=1 THEN AN#=VAL(AN$)
1390 IF VC=1 THEN V0#=V0#*-1:AN$=STR$(V0#):RETURN
1400 IF VC=0 THEN AN#=AN#*-1:VC=1:PF=0:AF=0:IF CA=-1 THEN V0#=AN#
1410 IF AN#=0 THEN RETURN
1420 IF MID$(AN$,1,1)="-" THEN AN$=MID$(AN$,2) ELSE AN$="-"+AN$
1430 RETURN
1440 IF A$="+" THEN CA=0:RETURN
1450 IF A$="-" THEN CA=1:RETURN
1460 IF A$="*" THEN CA=2:RETURN
1470 IF A$="/" THEN CA=3:RETURN
1480 CA=-1
1490 RETURN
1500 ' キーマッピングのデータ
1510 DATA "c","!","","/"
1520 DATA "7","8","9","*"
1530 DATA "4","5","6","-"
1540 DATA "1","2","3","+"
1550 DATA "0",".","","="
1560 ' テンキー描画文字データ
1570 DATA "AC",15,"",13 :' AC !   /
1580 DATA 7,8,9,12      :'  7 8 9 *
1590 DATA 4,5,6,11      :'  4 5 6 -
1600 DATA 1,2,3,10      :'  1 2 3 +
1610 DATA 0,20,"",14    :'  0 .   =