10 ' 計算機
20 ' 初期化
30 CLEAR 200,&HDD00
40 BLOAD"strbuf.bin"
50 DEF USR0=&HDD00:DEF USR1=&HDD03:DEF USR2=&HDD06
60 SCREEN 5,,0:COLOR 15,4,7:DEFINT A-Z:SET PAGE 0,1
70 DIM K(3,4)
80 X0=-1:Y0=-1:AN$="0":MS$=AN$:PF=0:VC=1:AF=0:CA=-1:MO=0:EQ=0:A=0:UP=1:TS=0
90 ' フォントデータの読み込み
100 COPY "font12.dat" TO (0,0):' 12x16フォント(0123456789+-×÷=±ACER. )
110 COPY "font16.dat" TO (0,32):' 16x16フォント(0123456789+-×÷=±ACER. )
120 COPY "font8.dat" TO (0,64):' 8x8フォント(+-×÷ )
130 SET PAGE 0,0
140 ' 画面上のテンキーに対応するキー情報を初期化
150 _TURBO ON(K())
160 RESTORE 1600
170 FOR Y=0 TO 4
180     FOR X=0 TO 3
190         READ A$
200         IF A$<>"" THEN K(X,Y)=ASC(A$) ELSE K(X,Y)=0
210     NEXT X
220 NEXT Y
230 ' 画面表示
240 FOR Y=27 TO 211 STEP 37
250     FOR X = 0 TO 255 STEP 64
260         LINE (0,Y)-(255,Y)
270         LINE (X,27)-(X, 211)
280         READ A$
290         IF A$="" THEN GOTO 320
300         IF A$="AC" THEN GOSUB 370:GOTO 320
310         GOSUB 420
320     NEXT X
330 NEXT Y
340 GOTO 440
350 ' "AC"を描画
360 ' x,y:テンキーの座標(4x5)
370 COPY(0,48)-STEP(15,15),1 TO (X+16,Y+10):COPY(16,48)-STEP(15,15),1 TO (X+32,Y+10)
380 RETURN
390 ' テンキーの文字を描画
400 ' x,y:テンキーの座標(4x5)
410 ' a:描画するこのプログラム内での文字コード
420 A=VAL(A$):XX=A*16 MOD 256:Y1=A*16:Y2=Y1/256:YY=Y2*16+32:COPY(XX,YY)-STEP(15,15),1 TO (X+24,Y+10)
430 RETURN
440 _TURBO OFF
450 ' キー入力&パッド入力
460 P=USR0(0):P=USR1(AN$)
470 P=USR0(1):P=USR1(MS$)
480 _TURBO ON(K(),A,X0,Y0,CA,EQ,VC,AF,UP)
490 P=USR0(0):P=USR2(VARPTR(AN$))
500 P=USR0(1):P=USR2(VARPTR(MS$))
510 IF UP=1 THEN GOSUB 840:UP=0
520 MO=0
530 P=PAD(4)
540 A$=INKEY$
550 IF X0<>-1 AND TIME-TS>30 THEN GOSUB 710:X0=-1
560 IF P<>0 THEN MO=1
570 IF A$<>"" THEN MO=2
580 IF MO=0 THEN 520
590 IF MO=1 THEN GOSUB 710:X0=PAD(5)/64:Y0=(PAD(6)-27)/37:A$=CHR$(K(X0,Y0)):TS=TIME:GOSUB 710
600 IF A$=CHR$(&H1B) THEN A$="c"
610 IF A$=CHR$(13) THEN A$="="
620 IF MO=2 THEN GOSUB 750:GOSUB 710
630 IF A$="" OR A$=CHR$(0) THEN 520
640 IF A$>="0" AND A$<="9" THEN GOSUB 1030:P$=AN$:GOSUB 1130:MS$=P$:UP=1:GOTO 510
650 IF A$="." AND PF=0 THEN PF=1:IF VC=1 THEN AN$="0.":VC=0:MS$=AN$:UP=1:GOTO 510 ELSE AN$=AN$+".":P$=AN$:GOSUB 1130:MS$=P$:UP=1:GOTO 510
660 A=ASC(A$)
670 GOTO 1170
680 ' ボタンの反転描画
690 ' x0,y0:テンキーの座標(4x5)
700 ' x0==-1の時はなにもしない。
710 IF X0=-1 THEN RETURN
720 LINE(X0*64+1,Y0*37+27)-STEP(62,35),15,BF,XOR
730 RETURN
740 ' キーチェック
750 KK=ASC(A$)
760 FOR Y=0 TO 4
770     FOR X=0 TO 3
780         IF K(X,Y)=KK THEN GOSUB 710:X0=X:Y0=Y:TS=TIME
790     NEXT X
800 NEXT Y
810 RETURN
820 ' 値を表示するエリアを再描画
830 ' ms$:描画する文字列
840 L=LEN(MS$)
850 SX=240-L*12:LINE(0,0)-(SX-1,26),4,BF
860 FOR I=1 TO L
870     C=ASC(MID$(MS$,I,1))
880     IF C=&H45 THEN X=216:Y=0:GOTO 950
890     IF C=&H52 THEN X=228:Y=0:GOTO 950                   
900     IF C>=&H30 THEN X=(C-&H30)*12:Y=0:GOTO 950
910     IF C=&H2B THEN X=120:Y=0:GOTO 950
920     IF C=&H2D THEN X=132:Y=0:GOTO 950
930     IF C=&H2E THEN X=240:Y=0:GOTO 950
940     IF C=&H20 THEN X=0:Y=16:GOTO 950
950     COPY(X,Y)-STEP(11,15),1 TO (SX, 4)
960     SX=SX+12
970 NEXT I
980 IF CA=-1 THEN B=32 ELSE B=CA*8
990 COPY(B,64)-STEP(7,7),1 TO (244,12)
1000 RETURN
1010 ' an$に入力した数値を追加する
1020 ' a$:追加する数値or少数点
1030 IF EQ=1 THEN CA=-1:EQ=0
1040 IF VC=1 THEN AN$=A$:AF=1:IF A$="0" THEN VC=1:GOTO 1100 ELSE VC=0:GOTO 1100
1050 LL=LEN(AN$):MM$=MID$(AN$,1,1)
1060 IF LL=14 AND MM$<>"-" THEN 1100
1070 IF LL=15 AND MM$="-" THEN 1100
1080 AN$=AN$+A$
1090 AF=1
1100 RETURN
1110 ' 少数点で始まる場合、前に0を追加する
1120 ' P$:数値の文字列
1130 IF MID$(P$,1,1)=" " THEN P$=MID$(P$,2)
1140 IF MID$(P$,1,1)="-" THEN SP$="-":P$=MID$(P$,2) ELSE SP$=""
1150 IF MID$(P$,1,1)="." THEN P$="0"+P$
1160 P$=SP$+P$:RETURN
1170 P=USR0(0):P=USR1(VARPTR(AN$))
1180 P=USR0(1):P=USR1(VARPTR(MS$))
1190 _TURBO OFF
1200 A$=CHR$(A)
1210 P=USR0(0):AN$=SPACE$(USR0(-1)):P=USR2(AN$)
1220 P=USR0(1):MS$=SPACE$(USR0(-1)):P=USR2(MS$)
1230 IF A$="*" OR A$="/" OR A$="+" OR A$="-" THEN EQ=0:IF VC=0 THEN VC=1:GOSUB 1300:GOTO 460 ELSE GOSUB 1530:UP=1:GOTO 460
1240 IF A$="c" THEN IF VC=0 THEN AN$="0":PF=0:VC=1:AF=0:MS$=AN$:UP=1:GOTO 460 ELSE AN$="0":PF=0:VC=1:CA=-1:V0#=0:AF=0:MS$=AN$:UP=1:GOTO 460
1250 IF A$="=" THEN EQ=1:VC=1:GOSUB 1300:GOTO 460
1260 IF A$="!" THEN GOSUB 1470:P$=AN$:GOSUB 1130:MS$=P$:UP=1:GOTO 460
1270 IF A$="q" THEN END
1280 GOTO 460
1290 ' 演算処理
1300 IF AF=1 THEN AN#=VAL(AN$)
1310 EF=0:' エラーフラグ
1320 IF CA=-1 THEN V0#=AN# ELSE IF A$<>"=" AND AF=0 THEN GOSUB 1540 ELSE GOSUB 1380
1330 VC=1:PF=0:AF=0
1340 IF EF=1 THEN MS$="ERR":UP=1:RETURN
1350 IF A$<>"=" THEN GOSUB 1530
1360 P$=STR$(V0#):GOSUB 1130:AN$=P$:MS$=P$:UP=1
1370 RETURN
1380 ON ERROR GOTO 1450
1390 IF CA=0 THEN V0#=V0#+AN#:GOTO 1430
1400 IF CA=1 THEN V0#=V0#-AN#:GOTO 1430
1410 IF CA=2 THEN V0#=V0#*AN#:GOTO 1430
1420 IF CA=3 THEN V0#=V0#/AN#:GOTO 1430
1430 ON ERROR GOTO 0
1440 RETURN
1450 EF=1:RESUME NEXT
1460 ' 符号反転の処理("!"を押した時)
1470 IF AF=1 THEN AN#=VAL(AN$)
1480 IF VC=1 THEN V0#=V0#*-1:AN$=STR$(V0#):RETURN
1490 IF VC=0 THEN AN#=AN#*-1:VC=1:PF=0:AF=0:IF CA=-1 THEN V0#=AN#
1500 IF AN#=0 THEN RETURN
1510 IF MID$(AN$,1,1)="-" THEN AN$=MID$(AN$,2) ELSE AN$="-"+AN$
1520 RETURN
1530 IF A$="+" THEN CA=0:RETURN
1540 IF A$="-" THEN CA=1:RETURN
1550 IF A$="*" THEN CA=2:RETURN
1560 IF A$="/" THEN CA=3:RETURN
1570 CA=-1
1580 RETURN
1590 ' キーマッピングのデータ
1600 DATA "c","!","","/"
1610 DATA "7","8","9","*"
1620 DATA "4","5","6","-"
1630 DATA "1","2","3","+"
1640 DATA "0",".","","="
1650 ' テンキー描画文字データ
1660 DATA "AC",15,"",13 :' AC !   /
1670 DATA 7,8,9,12      :'  7 8 9 *
1680 DATA 4,5,6,11      :'  4 5 6 -
1690 DATA 1,2,3,10      :'  1 2 3 +
1700 DATA 0,20,"",14    :'  0 .   =