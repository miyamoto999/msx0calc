10 ' 計算機
20 ' 初期化
30 SCREEN 5,,0:COLOR 15,4,7:DEFINT A-Z
40 OPEN "GRP:" FOR OUTPUT AS #1
50 DIM K$(3,4)
60 X0=-1:Y0=-1:AN$="0":PF=0:VC=1:AF=0:CA$="":MO=0:BM$="":EQ=0
70 ' 画面上のテンキーに対応するキー情報を初期化
80 FOR Y=0 TO 4
90     FOR X=0 TO 3
100         READ K$(X,Y)
110     NEXT
120 NEXT
130 ' 画面表示
140 FOR Y=52 TO 211 STEP 32
150     FOR X = 0 TO 255 STEP 64
160         LINE (0,Y)-(255,Y)
170         LINE (X,52)-(X, 211)
180         READ A$
190         IF A$="" THEN GOTO 220
200         IF A$="AC" THEN GOSUB 480:GOTO 220
210         GOSUB 530
220     NEXT
230 NEXT
240 MS$=AN$:GOSUB 630
250 ' キー入力&パッド入力
260 MO=0
270 P=PAD(4)
280 A$=INKEY$
290 IF X0<>-1 THEN GOSUB 580:X0=-1
300 IF P<>0 THEN MO=1
310 IF A$<>"" THEN MO=2
320 IF MO=0 THEN 260
330 IF A$=CHR$(&H1B) THEN A$="c"
340 IF A$="q" THEN END
350 IF A$=CHR$(13) THEN A$="="
360 IF MO=1 THEN X0=PAD(5)/64:Y0=(PAD(6)-52)/32:A$=K$(X0,Y0):GOSUB 580
370 IF MO=2 THEN GOSUB 900:GOSUB 580
380 IF A$="" THEN GOTO 260
390 IF A$>="0" AND A$<="9" THEN GOSUB 780:P$=AN$:GOSUB 990:MS$=RT$:GOSUB 630
400 IF A$="." AND PF=0 THEN PF=1:IF VC=1 THEN AN$="0.":VC=0:MS$=AN$:GOSUB 630 ELSE AN$=AN$+".":P$=AN$:GOSUB 990:MS$=RT$:GOSUB 630
410 IF A$="c" THEN IF VC=0 THEN AN$="0":PF=0:VC=1:AF=0:MS$=AN$:GOSUB 630 ELSE AN$="0":PF=0:VC=1:CA$="":V0#=0:AF=0:MS$=AN$:GOSUB 630
420 IF A$="*" OR A$="/" OR A$="+" OR A$="-" OR A$="=" THEN EQ=0:IF VC=0 THEN VC=1:GOSUB 1260 ELSE IF A$<>"=" THEN CA$=A$:GOSUB 630 ELSE GOSUB 1260
430 IF A$="=" THEN EQ=1
440 IF A$="!" THEN GOSUB 1430:P$=AN$:GOSUB 990:MS$=RT$:GOSUB 630
450 GOTO 260
460 ' "AC"を描画
470 ' x,y:テンキーの座標(4x5)
480 PUT KANJI(X+16,Y+8),&H2341:PUT KANJI(X+32,Y+8),&H2343
490 RETURN
500 ' テンキーの文字を描画
510 ' x,y:テンキーの座標(4x5)
520 ' a:描画する文字のJIS漢字コード
530 A=VAL("&h"+A$):PUT KANJI(X+24,Y+8),A
540 RETURN
550 ' ボタンの反転描画
560 ' x0,y0:テンキーの座標(4x5)
570 ' x0==-1の時はなにもしない。
580 IF X0=-1 THEN RETURN
590 LINE(X0*64+1,Y0*32+53)-STEP(62,30),15,BF,XOR
600 RETURN
610 ' 値を表示するエリアを再描画
620 ' ms$:描画する文字列
630 IF LEN(BM$)>LEN(MS$) THEN GOSUB 860 
640 FOR I=1 TO LEN(MS$)
650     C$=MID$(MS$,I,1)
660     IF C$="." THEN A=&H2125:GOTO 710
670     IF C$="-" THEN A=&H215D:GOTO 710
680     IF C$="+" THEN A=&H215C:GOTO 710
690     IF C$=" " THEN A=&H2121:GOTO 710
700     A=ASC(C$)+&H2300
710     PUT KANJI(256-LEN(MS$)*16+(I-1)*16,8),A
720 NEXT
730 PSET(247, 44),4:IF CA$="" THEN PRINT #1," " ELSE PRINT #1,CA$
740 BM$=MS$
750 RETURN
760 ' an$に入力した数値を追加する
770 ' a$:追加する数値or少数点
780 IF EQ=1 THEN CA$="":EQ=0
790 IF VC=1 THEN AN$=A$:GOSUB 860:AF=1:IF A$="0" THEN VC=1:RETURN ELSE VC=0:RETURN
800 IF LEN(AN$)=14 AND INSTR(AN$,".")=0 THEN RETURN
810 IF LEN(AN$)=15 THEN RETURN
820 AN$=AN$+A$
830 AF=1
840 RETURN
850 ' 値表示エリアをクリア
860 LINE(0,0)-(255,51),4,BF
870 BM$=""
880 RETURN
890 ' キーチェック
900 FOR Y=0 TO 4
910     FOR X=0 TO 3
920         IF K$(X,Y)=A$ THEN X0=X:Y0=Y:GOTO 940
930     NEXT
940 NEXT
950 RETURN
960 ' 数値の文字列を16文字以内におさめる
970 ' P$:数値の文字列
980 ' 結果:RT$:変換後の文字列
990 IF MID$(P$,1,1)=" " THEN P$=MID$(P$,2)
1000 IF MID$(P$,1,1)="-" THEN SP$="-":P$=MID$(P$,2) ELSE SP$=""
1010 IF MID$(P$,1,1)="." THEN P$="0"+P$
1020 IF LEN(SP$)+LEN(P$)<=16 THEN RT$=SP$+P$:RETURN
1030 I=INSTR(P$,"."):IF I<>0 THEN IP$=MID$(P$,1,I-1) ELSE IP$=""
1040 J=INSTR(P$,"E"):IF J<>0 THEN DP$=MID$(P$,I+1,J-I-1):EP=VAL(MID$(P$,J+1)) ELSE DP$=MID$(P$,I+1):EP=0
1050 IF LEN(IP$)=1 AND MID$(IP$,1,1)="0" THEN GOSUB 1110 ELSE GOSUB 1200
1060 IF EP<>0 THEN IF SP$="" THEN DP$=LEFT$(DP$,10) ELSE DP$=LEFT$(DP$,9)
1070 RT$=SP$+IP$+"."+DP$
1080 IF EP=0 THEN RT$=LEFT$(RT$,16) ELSE IF EP>0 THEN RT$=RT$+"E+"+MID$(STR$(EP),2) ELSE RT$=RT$+"E"+STR$(EP) 
1090 RETURN
1100 ''''
1110 K=1
1120 IF MID$(DP$,K,1)="0" THEN K=K+1:GOTO 1120
1130 K=K-1
1140 EP=EP-(K+1)
1150 DP$=MID$(DP$,K+1)
1160 IP$=MID$(DP$,1,1)
1170 DP$=MID$(DP$,2)
1180 RETURN
1190 ''''
1200 IF LEN(IP$)<2 THEN RETURN
1210 DP$=MID$(IP$,2)+DP$
1220 EP=EP+LEN(IP$)-1
1230 IP$=MID$(IP$,1,1)
1240 RETURN
1250 ' 演算処理
1260 IF AF=1 THEN AN#=VAL(AN$)
1270 EF=0:' エラーフラグ
1280 IF CA$="" THEN V0#=AN# ELSE IF A$<>"=" AND AF=0 THEN CA$=A$ ELSE GOSUB 1340
1290 VC=1:PF=0:AF=0
1300 IF EF=1 THEN MS$="ERR":GOSUB 860:GOSUB 630:RETURN
1310 IF A$<>"=" THEN CA$=A$
1320 P$=STR$(V0#):GOSUB 990:AN$=RT$:MS$=RT$:GOSUB 630
1330 RETURN
1340 ON ERROR GOTO 1410
1350 IF CA$="-" THEN V0#=V0#-AN#
1360 IF CA$="*" THEN V0#=V0#*AN#
1370 IF CA$="+" THEN V0#=V0#+AN#
1380 IF CA$="/" THEN V0#=V0#/AN#
1390 ON ERROR GOTO 0
1400 RETURN
1410 EF=1:RESUME NEXT
1420 ' 符号反転の処理("!"を押した時)
1430 IF AF=1 THEN AN#=VAL(AN$)
1440 IF VC=1 THEN V0#=V0#*-1:AN$=STR$(V0#):RETURN
1450 IF VC=0 THEN AN#=AN#*-1:VC=1:PF=0:AF=0
1460 IF AN#=0 THEN RETURN
1470 IF MID$(AN$,1,1)="-" THEN AN$=MID$(AN$,2) ELSE AN$="-"+AN$
1480 RETURN
1490 ' キーマッピングのデータ
1500 DATA "c","!","","/"
1510 DATA "7","8","9","*"
1520 DATA "4","5","6","-"
1530 DATA "1","2","3","+"
1540 DATA "0",".","","="
1550 ' テンキー描画文字データ
1560 DATA "AC",215E,"",2160		:' AC !   /
1570 DATA 2337,2338,2339,215f	:'  7 8 9 *
1580 DATA 2334,2335,2336,215d	:'  4 5 6 -
1590 DATA 2331,2332,2333,215c	:'  1 2 3 +
1600 DATA 2330,2125,"",2161		:'  0 .   =